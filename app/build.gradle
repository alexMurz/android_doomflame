//file:noinspection SpellCheckingInspection
plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 33
    buildToolsVersion '33.0.0'
    ndkVersion '25.1.8937393'

    defaultConfig {
        applicationId "com.example.doomflame"
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace 'com.example.doomflame'
    buildFeatures {
        viewBinding true
    }
}

/**
 * Compile shaders
 */
task compileShaders {
    doLast {
        exec {
            workingDir("./src/main/flamenative/assets_compile")
            commandLine "cargo", "run"
        }
    }
}

/**
 * Build $project/libdoomflame in release configuration
 * And put resulting libraries into apps jniLibs
 * Targets: x64(emulator), armv7, arm64
 */
task cargoBuild {
    doLast {
        exec {
            workingDir("./src/main/flamenative")
            commandLine "cargo", "ndk",
                    "-t", "armeabi-v7a", // i686
                    "-t", "arm64-v8a", // aarch64
                    "-t", "x86_64", // x86_64
                    "-o", "..\\jniLibs",
                    "build", "--release"
        }
    }
}

// Any build task depends on cargoBuild
tasks.cargoBuild.dependsOn(compileShaders)
tasks.preBuild.dependsOn(cargoBuild)

dependencies {

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation "io.reactivex.rxjava3:rxjava:3.0.0"
    implementation "io.reactivex.rxjava3:rxandroid:3.0.0"
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
}